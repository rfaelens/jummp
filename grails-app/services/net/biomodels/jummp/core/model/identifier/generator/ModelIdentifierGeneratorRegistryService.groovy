/**
 * Copyright (C) 2010-2014 EMBL-European Bioinformatics Institute (EMBL-EBI),
 * Deutsches Krebsforschungszentrum (DKFZ)
 *
 * This file is part of Jummp.
 *
 * Jummp is free software; you can redistribute it and/or modify it under the
 * terms of the GNU Affero General Public License as published by the Free
 * Software Foundation; either version 3 of the License, or (at your option) any
 * later version.
 *
 * Jummp is distributed in the hope that it will be useful, but WITHOUT ANY
 * WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR
 * A PARTICULAR PURPOSE. See the GNU Affero General Public License for more
 * details.
 *
 * You should have received a copy of the GNU Affero General Public License along
 * with Jummp; if not, see <http://www.gnu.org/licenses/agpl-3.0.html>.
 **/

package net.biomodels.jummp.core.model.identifier.generator

import org.apache.commons.logging.Log
import org.apache.commons.logging.LogFactory

/**
 * @short Service for registering and storing model identifier generators.
 *
 * @author Mihai Glon»õ <mihai.glont@ebi.ac.uk>
 */
class ModelIdentifierGeneratorRegistryService {
    // No interaction with the database, so disable the proxy providing transactional behaviour
    static transactional = false

    /* the class logger */
    private static final Log log = LogFactory.getLog(this)
    /* semaphore for the log threshold */
    private static final boolean IS_INFO_ENABLED = log.isInfoEnabled()
    /* association between a model id generator and its corresponding name */
    private Map<String, ModelIdentifierGenerator> registry = [:]

    /**
     * Adds @p generator to the registry under the name @p name.
     * Returns false if any of the arguments is undefined or empty, or if there is already a
     * generator in the registry with name @p name.
     */
    boolean registerGenerator(String name, ModelIdentifierGenerator generator) {
        if (!name || !generator) {
            String e = "Generators must have a name and not be null in order to be registered."
            log.warn(e)
            return false
        }
        if (registry[name]) {
            String e = "A modelIdGenerator named $name already exists."
            log.warn(e)
            return false
        }
        if (IS_INFO_ENABLED) {
            log.info "Registering ${generator.getClass().getName()} as $name"
        }
        registry[name] = generator
        return true
    }

    /**
     * Asks generators in the registry to prepare for providing a new model identifier.
     */
    void updateGenerators() {
        if (IS_INFO_ENABLED) {
            log.info("Triggering an update of all generators: ${registry.inspect()}")
        }
        registry.each { g -> g.updateDecorators() }
        if (IS_INFO_ENABLED) {
            log.info("Generators have been updated.")
        }
    }

    /**
     * Returns the generator associated in the registry with @p name, or null if nothing was found.
     */
    ModelIdentifierGenerator findByName(String name) {
        ModelIdentifierGenerator result = registry[name]
        if (IS_INFO_ENABLED) {
            log.info("Searched for a generator named $name and found ${result ?: 'nothing'}.")
        }
        return result
    }
}
